//
// Find BerkeleyDB JAR
//
def subdirs
if (isWindows) {
    rootProject.nugetRestore.execute()
    subdirs = [ "${rootProject.projectDir}/packages/berkeley.db.java7.5.3.28.1/build/native/lib/" ]
} else {
    subdirs = [
        "/usr/local/opt/ice/libexec/lib/",
        "/usr/local/opt/berkeley-db@5.3/lib/",
        "/usr/local/lib/",
        "/usr/share/java/",
        "/opt/Ice-${project.version}/lib/",
        "/opt/db/lib/",
        "/usr/lib/"
    ]
}

def candidates = ["db-5.3.28.jar", "db-5.3.21.jar", "db.jar"].collect {
    def dbJarName = it
    subdirs.collect {
        it.concat(dbJarName)
    }
}.flatten()

ext.dbJar = candidates.find {
    new File(it).exists()
}

if(!ext.dbJar || !new File(ext.dbJar).exists()) {
    throw new GradleException("Unable to locate the required third party BerkeleyDB jar file `db.jar'.")
}
